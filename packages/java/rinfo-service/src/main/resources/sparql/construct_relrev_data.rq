PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rpubl: <http://rinfo.lagrummet.se/ns/2008/11/rinfo/publ#>

CONSTRUCT {

    ?s ?p ?o .
    ?current ?curr_p ?curr_o .

    ?relitem a ?reltype;
        ?relprop ?reltext .

    ?reldoc rpubl:paragraf ?relitem .
    ?relchapter rpubl:paragraf ?relitem .
    ?reldoc rpubl:kapitel ?relchapter .
    ?reldoc a ?reldoctype;
        dct:identifier ?reldoc_label .


    ?revdoc a ?revdoctype;
        dct:identifier ?revdoc_id .

    ?revitem a ?revtype;
        ?rev ?current;
        ?revprop ?revtext .

    ?revdoc rpubl:paragraf ?revitem .
    ?revchapter rpubl:paragraf ?revitem .
    ?revdoc rpubl:kapitel ?revchapter .


    ?inrefitem a rpubl:Forfattningsreferens;
        rpubl:angerGrundforfattning ?current;
        ?refprop ?reftext .

    ?revdoc ?refRev ?inrefitem .
    ?revdoc rpubl:kapitel ?revchapter .
    ?revchapter rpubl:paragraf ?revpara .
    ?revdoc rpubl:paragraf ?revpara .
    ?revpara ?paraRefRev ?inrefitem .

} WHERE {

    {

        ?g foaf:primaryTopic ?current .
        GRAPH ?g { ?s ?p ?o }

    } UNION {

        ?current ?curr_p ?curr_o .

    } UNION {

        ?current ?rel ?relitem .
        ?relitem ?relprop ?reltext .
        FILTER( isLiteral(?reltext) )
        OPTIONAL { ?relitem a ?reltype . }

        # TODO:IMPROVE: generalize...
        # NOTE: this part is a bit slow:
        OPTIONAL {
            {
                ?reldoc rpubl:paragraf ?relitem .
            } UNION {
                ?relchapter rpubl:paragraf ?relitem .
                ?reldoc rpubl:kapitel ?relchapter .
            # TODO: currect construct for this
            #} UNION {
            #    ?relitem a rpubl:Forfattningsreferens;
            #        rpubl:angerGrundforfattning ?reldoc .
            }
            ?reldoc a ?reldoctype;
                dct:identifier ?reldoc_label .
        }

    } UNION {

        ?revitem ?rev ?current; ?revprop ?revtext .
        FILTER( isLiteral(?revtext) )
        OPTIONAL { ?revitem a ?revtype . }

        # Don't get revs if ?current may have lots of "parts"...
        # TODO: optimize!
        ?current a ?type .
        OPTIONAL {
            ?type rdfs:subClassOf ?supertype .
            OPTIONAL { ?supertype rdfs:subClassOf ?supersupertype . }
        }
        FILTER(
            ?type != foaf:Organization &&
            ?type != bibo:Collection &&
            ?supertype != bibo:Collection &&
            ?supersupertype != bibo:Collection)

        # TODO:IMPROVE: generalize...
        # - follow revitems upward to containers and describe those
        # - filter scovo:Item?
        # - don't get (isPartOf?) rev for collections
        # NOTE: this part is a bit slow:
        OPTIONAL {
            {
                ?revdoc rpubl:paragraf ?revitem .
            } UNION {
                ?revchapter rpubl:paragraf ?revitem .
                ?revdoc rpubl:kapitel ?revchapter .
            }
            ?revdoc a ?revdoctype;
                dct:identifier ?revdoc_label .
        }

    } UNION {

        ?inrefitem a rpubl:Forfattningsreferens;
            rpubl:angerGrundforfattning ?current;
            ?refprop ?reftext .
        {
            ?revdoc rpubl:kapitel ?revchapter .
            ?revchapter rpubl:paragraf ?revpara .
            ?revpara ?paraRefRev ?inrefitem .
        } UNION {
            ?revdoc rpubl:paragraf ?revpara .
            ?revpara ?paraRefRev ?inrefitem .
        } UNION {
            ?revdoc ?refRev ?inrefitem .
        }

        ?revdoc a ?revdoctype;
            dct:identifier ?revdoc_id .

    }

}
