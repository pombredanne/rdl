PREFIX : <http://rinfo.lagrummet.se/ns/2008/11/rinfo/publ#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX awol: <http://bblfish.net/work/atom-owl/2006-06-06/#>
PREFIX iana: <http://www.iana.org/assignments/relation/>

CONSTRUCT {

    ?entry a awol:Entry;
        awol:updated ?entryUpdated;
        awol:content ?content;
        iana:alternate ?alternate;
        foaf:primaryTopic ?current .
    ?content awol:type ?mediaType .
    ?alternate awol:type ?mediaType .

    ?current ?curr_p ?curr_o .

    ?konsolidering :konsoliderar ?current .
    ?konsolidering a ?rev_type;
        dct:identifier ?rev_identifier;
        dct:issued ?rev_issued .

    ?andrande :andrar ?current .
    ?andrande a ?rev_type;
        dct:identifier ?rev_identifier;
        :ikrafttradandedatum ?rev_ikrafttradandedatum .

    ?upphavande :upphaver ?current .
    ?upphavande a ?rev_type;
        dct:identifier ?rev_identifier;
        :ikrafttradandedatum ?rev_ikrafttradandedatum .

} WHERE {

    #FILTER(?current = <$CURRENT>)

    {

        ?current ?curr_p ?curr_o .
        FILTER(isLiteral(?curr_o))

    } UNION {

        ?current ?curr_p ?curr_o .
        FILTER(?curr_p in (rdf:type,
                            dct:publisher,
                            :departement,
                            :forfattningssamling,
                            :utrSerie,
                            :rattsfallspublikation,
                            :allmannaRadSerie,
                            :andrar,
                            :ersatter,
                            :upphaver,
                            :konsoliderar,
                            :omtryckAv,
                            :lagrum,
                            :bemyndigande,
                            :genomforDirektiv,
                            :forarbete,
                            :foreslarAndringAv,
                            :utgarFran))

    } UNION {

        ?entry a awol:Entry;
            foaf:primaryTopic ?current;
            awol:updated ?entryUpdated .
        OPTIONAL {
            ?entry awol:content ?content .
            ?content awol:type ?mediaType .
        }
        OPTIONAL {
            ?entry iana:alternate ?alternate .
            ?alternate awol:type ?mediaType .
        }

    } UNION {

        ?konsolidering :konsoliderar ?current;
            a ?rev_type;
            dct:identifier ?rev_identifier;
            dct:issued ?rev_issued .

        FILTER NOT EXISTS {
            ?konsolidering2 :konsoliderar ?current;
                dct:issued ?rev_issued2 .
            FILTER(?rev_issued2 > ?rev_issued)
        }

    } UNION {

        ?andrande :andrar ?current;
            a ?rev_type;
            dct:identifier ?rev_identifier .
        OPTIONAL {
            ?andrande :ikrafttradandedatum ?rev_ikrafttradandedatum .
        }

        FILTER NOT EXISTS {
            ?andrande2 :andrar ?current;
                :ikrafttradandedatum ?rev_ikrafttradandedatum2 .
            FILTER(?rev_ikrafttradandedatum2 > ?rev_ikrafttradandedatum)
        }

    } UNION {

        ?upphavande :upphaver ?current;
            a ?rev_type;
            dct:identifier ?rev_identifier .
        OPTIONAL {
            ?upphavande :ikrafttradandedatum ?rev_ikrafttradandedatum .
        }

    }

}
