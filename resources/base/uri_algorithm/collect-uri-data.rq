PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX sioc: <http://rdfs.org/sioc/ns#>
PREFIX rpubl: <http://rinfo.lagrummet.se/ns/2008/11/rinfo/publ#>
PREFIX rkmt: <http://rinfo.lagrummet.se/ns/2008/11/rinfo/kommitte#>


SELECT
    ?about ?containerId
    ?rattsdokumentnummer
    ?konsoliderar ?konsDatum

#FROM <current.rdf>
#FROM <http://rinfo.lagrummet.se/ns/2008/01/containers>

WHERE {


    #== By membership in some publication series ==

    {
        ?about a ?type; # rpubl:FSDokument
            rpubl:forfattningssamling ?_fs;
            rpubl:fsNummer ?rattsdokumentnummer .
        ?containerUri a sioc:Container;
            sioc:has_host <http://rinfo.lagrummet.se>;
            foaf:primaryTopic ?_fs;
            sioc:id ?containerId .
    }

    UNION
    {
        ?about a ?type; # rpubl:Utredningsbetankande
            rpubl:utrSerie ?_utrSerie;
            rpubl:utrSerienummer ?rattsdokumentnummer .
        ?containerUri
            foaf:primaryTopic ?_utrSerie;
            sioc:id ?containerId .
    }

    UNION
    {
        ?about a rpubl:Rattsfallsreferat;
            rpubl:rattsfallspublikation ?_rfpubl;
            rpubl:publikationsordinal ?rattsdokumentnummer .
            # FIXME: eventuellt 채ven arsutgava, sidnummer?
            # *men* ibland publiceras de inte som referat; d책 referera som
            # domstol+m책lnummer+datum.. (se modellens kommentarer)
        ?containerUri
            foaf:primaryTopic ?_rfpubl;
            sioc:id ?containerId .
    }


    #== By relationship to other doc and date ==

    UNION
    {
        ?about a rpubl:KonsolideradGrundforfattning;
            rpubl:konsoliderar ?konsoliderar;
            rpubl:utfardandedatum ?konsDatum .
            # FIXME: utkomFranTryck? kosolideringsdatum? dct:issued? N책gon slags ordinal?
    }

    # FIXME: ok med rattningAv + beslutsdatum?
    #UNION
    #{
    #    ?about a rpubl:Rattelseblad;
    #        rpubl:rattningAv ?rattningAv;
    #        rpubl:beslutsdatum ?rattnBeslutsdatum .
    #}


    #== By publisher and subPropertyOf rattsdokumentnummer ==
    UNION
    {
        ?about a rpubl:VagledandeMyndighetsavgorande;
            dct:publisher ?_publisher;
            rpubl:diarienummer ?rattsdokumentnummer .
        ?containerUri
            # TODO: more specific? by type or property?
            # TODO: by specific type for sub-partitions?
            foaf:primaryTopic ?_publisher;
            #sioc:topic dct:publisher;
            foaf:topic foaf:Document;
            sioc:id ?containerId .
    }


    #== By publisher and special number property ==
    UNION
    {
        ?about a rpubl:VagledandeDomstolsavgorande;
            dct:publisher ?_publisher;
            rpubl:malnummer ?rattsdokumentnummer .
        ?containerUri
            foaf:primaryTopic ?_publisher;
            # FIXME: more specific topic? by type or property?
            foaf:topic foaf:Document;
            sioc:id ?containerId .
    }


    #== By type and subPropertyOf rattsdokumentnummer ==

    UNION
    {
        ?about a rpubl:Proposition;
            rpubl:propositionsnummer ?rattsdokumentnummer .
        ?containerUri
            foaf:primaryTopic rpubl:Proposition;
            sioc:id ?containerId .
    }

    UNION
    {
        ?about a rpubl:Forordningsmotiv;
            rpubl:fmNummer ?rattsdokumentnummer .
        ?containerUri
            foaf:primaryTopic rpubl:Forordningsmotiv;
            sioc:id ?containerId .
    }

    UNION
    {
        ?about a rpubl:Kommittedirektiv;
            rpubl:direktivnummer ?rattsdokumentnummer .
        ?containerUri
            foaf:primaryTopic rpubl:Kommittedirektiv;
            sioc:id ?containerId .
    }

    UNION
    {
        ?about a rpubl:InternationellOverenskommelse;
            rpubl:soNummer ?rattsdokumentnummer .
        ?containerUri
            foaf:primaryTopic rpubl:InternationellOverenskommelse;
            sioc:id ?containerId .
    }


}

