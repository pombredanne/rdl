PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>

SELECT DISTINCT * WHERE {

    ?restr a owl:Restriction;
        owl:onProperty ?restr__prop .
    {
        ?restr owl:minCardinality ?restr__number .
    } UNION {
        ?restr owl:cardinality ?restr__number .
    }
    ?resource a ?resource__type .
    {
        ?restr__prop rdfs:domain ?resource__type .
    } UNION {
        ?resource__type rdfs:subClassOf ?restr .
    # NOTE: not necessary if storage/query engine does inference
    }# UNION {
    #    ?resource__type rdfs:subClassOf [ rdfs:subClassOf ?restr ] .
    #}
    OPTIONAL {
        ?resource ?restr__prop ?restr__prop__value .
    }
    FILTER( !BOUND(?restr__prop__value) )
    FILTER( ?restr__number > 0 )

}
