PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX sch: <http://purl.org/net/schemarama#>

CONSTRUCT {

    [] a sch:Warning;
        rdfs:isDefinedBy <http://rinfo.lagrummet.se/sys/validation/expected_lang.rq>;
        sch:message "Resurs [1]: förväntade något värde för egenskap [2] på svenska, fann [3]."@sv;
        sch:implicated (?thing ?prop ?value);
        dct:source ?context .

} WHERE {
    GRAPH ?context {
        ?thing ?prop ?value .
        FILTER(?prop in (dct:title, dct:description))
        MINUS {
            ?thing ?prop ?anyvalue .
            FILTER(langMatches(lang(?anyvalue), 'sv'))
        }
    }
}
